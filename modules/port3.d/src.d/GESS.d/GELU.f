       SUBROUTINE GELU(N,A,IA,INTER,EPS)
       INTEGER N,INTER(N)
       REAL A(IA,N),T,EPS,X
C THIS SUBROUTINE USES GAUSSIAN ELIMINATION WITH PARTIAL
C PIVOTING TO DECOMPOSE A MATRIX A INTO P*A=L*U WHERE
C P IS A PERMUTATION MATRIX,L IS A LOWER UNIT TRIANGULAR
C MATRIX, AND U IS AN UPPER TRIANGULAR MATRIX
C INPUT PARAMETERS
C     N      ORDER OF THE PROBLEM
C     A      AN IA X N ARRAY CONTAINING THE MATRIX TO BE
C            FACTORED
C     IA     ROW DIMENSION OF THE A ARRAY
C     EPS    REAL VARIABLE,IF THE MAGNITUDE OF THE PIVOT IS
C            LESS THAN OR EQUAL TO EPS,THE MATRIX IS
C            CONSIDERED SINGULAR
C OUTPUT PARAMETERS
C     A      CONTAINS THE UPPER TRIANGULAR MATRIX U AND A
C            PERMUTED VERSION OF THE LOWER TRIANGULAR MATRIX I-L
C     INTER  AN INTEGER VECTOR WHOSE ITH COMPONENT RECORDS THE
C            ITH PIVOT ROW, WITH INTER(N)=(-1)**NUMBER OF INTERCHANGES
C THE SUBROUTINES ISAMAX,SSCAL AND SSWAP ARE CALLED
C ERROR CONDITIONS
C      1      N IS LESS THAN 1   FATAL
C      2      IA.LT.N       FATAL
C      10+K   SINGULAR MATRIX OF RANK K    RECOVERABLE
C
C/6S
        IF(N.LT.1)CALL SETERR(12H GELU-N.LT.1,12,1,2)
        IF(IA.LT.N)CALL SETERR(13H GELU-IA.LT.N,13,2,2)
C/7S
C       IF(N.LT.1)CALL SETERR(' GELU-N.LT.1',12,1,2)
C       IF(IA.LT.N)CALL SETERR(' GELU-IA.LT.N',13,2,2)
C/
        CALL ENTER(1)
        IERR=0
        INTER(N)=1
        IF(N.EQ.1) GO TO 150
        NM1=N-1
        DO 100 K=1,NM1
           KP1=K+1
C DETERMINE THE PIVOT FOR THE KTH TRANSFORMATION
           M=ISAMAX(N-K+1,A(K,K),1)+K-1
           IF (K.EQ.M) GO TO 5
           INTER(N) = -INTER(N)
           CALL SSWAP(N-K+1,A(K,K),IA,A(M,K),IA)
 5         CONTINUE
C CHECK FOR NEAR SINGULARITY
           IF(ABS(A(K,K)).GT.EPS) GO TO 10
C/6S
           IF (IERR.EQ.0)
     1        CALL SETERR(21H GELU-SINGULAR MATRIX,21,9+K,1)
C/7S
C          IF (IERR.EQ.0)
C    1        CALL SETERR(' GELU-SINGULAR MATRIX',21,9+K,1)
C/
              IERR=1
           A(K,K)=SIGN(EPS,A(K,K))
 10        INTER(K)=M
C APPLY THE TRANSFORMATION TO THE MATRIX
            X=-A(K,K)
           IF(X.EQ.0.0)GO TO 100
           DO 20 J=KP1,N
              A(J,K)=A(J,K)/X
 20        CONTINUE
           DO 50 J=KP1,N
              T=A(K,J)
              IF(T.EQ.0.0) GO TO 50
              DO 40 I=KP1,N
                 A(I,J)=A(I,J)+T*A(I,K)
 40           CONTINUE
 50        CONTINUE
 100    CONTINUE
 150    IF (ABS(A(N,N)).GT.EPS)GO TO 160
C/6S
        IF (IERR.EQ.0)
     1        CALL SETERR(21H GELU-SINGULAR MATRIX, 21, 9+N, 1)
C/7S
C       IF (IERR.EQ.0)
C    1        CALL SETERR(' GELU-SINGULAR MATRIX', 21, 9+N, 1)
C/
           A(N,N)=SIGN(EPS,A(N,N))
 160   CONTINUE
        CALL LEAVE
        RETURN
        END
