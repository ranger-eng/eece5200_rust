      SUBROUTINE DGECE(N, A, IA, INTER, COND)
      INTEGER IA, N, INTER(N)
      DOUBLE PRECISION A(IA, N), COND
      COMMON /CSTAK/ R
      INTEGER  NERR, ISTKGT, NERROR, I,  IWORK
      DOUBLE PRECISION R(500), DASUM, SNORM, D1MACH
C THIS SUBROUTINE CALLS THE DECOMPOSITION ROUTINE AND DETERMINES
C AN ESTIMATE OF THE CONDITION NUMBER OF A D.P. DGENERAL MATRIX
C INPUT PARAMETERS
C    N        ORDER OF THE PROBLEM
C    A        MATRIX WHOSE CONDITION NUMBER IS REQUIRED
C    IA       ROW DIMENSION OF A
C OUTPUT PARAMETERS
C    A        CONTAINS THE LU DECOMPOSITION OF THE A MATRIX
C    INTER    PIVOT VECTOR PRODUCED BY THE DECOMPOSITION
C    COND     AN ESTIMATE OF THE CONDITION NUMBER OF THE MATRIX A
C EXTRA STORAGE ALLOCATED : N DOUBLE PRECISION LOCATIONS
C THE SUBROUTINES DASUM, DGELU, AND G4ECE ARE CALLED
C ERROR CONDITIONS-
C    1     N.LT.1       FATAL
C    2    IA.LT.N       FATAL
      CALL ENTER(1)
C/6S
      IF (N .LT. 1) CALL SETERR(13HDGECE-N.LT.1 , 13, 1, 2)
      IF (IA .LT. N) CALL SETERR(13HDGECE-IA.LT.N, 13, 2, 2)
C/7S
C     IF (N .LT. 1) CALL SETERR('DGECE-N.LT.1 ', 13, 1, 2)
C     IF (IA .LT. N) CALL SETERR('DGECE-IA.LT.N', 13, 2, 2)
C/
      SNORM = 0.D0
      DO  1 I = 1, N
         SNORM = DMAX1(SNORM, DASUM(N, A(1, I), 1))
   1     CONTINUE
      CALL DGELU(N,A,IA,INTER,0.D0)
      IF (NERROR(NERR) .EQ. 0) GOTO 2
         COND = D1MACH(2)
         GOTO  3
   2     IWORK = ISTKGT(N,4)
         CALL DG4ECE(N,A,IA,SNORM,COND,INTER,R(IWORK))
   3  CALL LEAVE
      RETURN
      END
