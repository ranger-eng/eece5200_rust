      SUBROUTINE  CSROT (N,CX,INCX,CY,INCY,C,S)
C
C     APPLIES A PLANE ROTATION, WHERE THE COS AND SIN (C AND S) ARE REAL
C     AND THE VECTORS CX AND CY ARE COMPLEX.
C     JACK DONGARRA, LINPACK, 3/11/78.
C
C     ADAPTED TO PORT 3 BY PHYL FOX, 11/8/83
C
      COMPLEX CX(1),CY(1),CTEMP
      REAL C,S
      INTEGER I,INCX,INCY,IX,IY,N
C
      IF(N.EQ.0)RETURN
C/6S
      IF(N .LT. 0) CALL SETERR(12HCSROT-N.LT.0, 12, 1, 2)
      IF(INCX .EQ. 0) CALL SETERR(15HCSROT-INCX.EQ.0, 15, 1, 2)
      IF(INCY .EQ. 0) CALL SETERR(15HCSROT-INCY.EQ.0, 15, 1, 2)
C/7S
C     IF(N .LT. 0) CALL SETERR('CSROT-N.LT.0', 12, 1, 2)
C     IF(INCX .EQ. 0) CALL SETERR('CSROT-INCX.EQ.0', 15, 1, 2)
C     IF(INCY .EQ. 0) CALL SETERR('CSROT-INCY.EQ.0', 15, 1, 2)
C/
C
      IF(INCX.EQ.1.AND.INCY.EQ.1)GO TO 20
C
C       CODE FOR UNEQUAL INCREMENTS OR EQUAL INCREMENTS NOT EQUAL
C         TO 1
C
      IX = 1
      IY = 1
      IF(INCX.LT.0)IX = (-N+1)*INCX + 1
      IF(INCY.LT.0)IY = (-N+1)*INCY + 1
      DO 10 I = 1,N
        CTEMP = C*CX(IX) + S*CY(IY)
        CY(IY) = C*CY(IY) - S*CX(IX)
        CX(IX) = CTEMP
        IX = IX + INCX
        IY = IY + INCY
   10 CONTINUE
      RETURN
C
C       CODE FOR BOTH INCREMENTS EQUAL TO 1
C
   20 DO 30 I = 1,N
        CTEMP = C*CX(I) + S*CY(I)
        CY(I) = C*CY(I) - S*CX(I)
        CX(I) = CTEMP
   30 CONTINUE
      RETURN
      END
